#! NX/KF 4.0

DefClass: swd_geom_contour_revolve (ug_base_part);
(List Modifiable Parameter) 						section_curves:							{};
(Integer Modifiable Parameter) 					hidden_layer: 							240;

# used to prevent the layer manager from executing twice
(Boolean Modifiable)										layer_manager_intialized?: 	false;

# collects the curves
(Child) section: {
  Class, ug_section,
  Tolerances, {0.0254, # results of adopting a section
							 0.02413,
								0.5},
  Chain_Rules, {Feature_Curves} + section_curves:,
  Start_Point_Direction, {},
  %Removed_Output, {},
  %Added_Output, {},
  Output_Map, {}
};

# helper object
# for axis creation
(Child) Direction: {
  Class, nx_direction,
  Vector, Vector(1,0,0),
  Origin, Point(0,0,0),
  Visibility, invisible,
  Color, 0,
  Layer, hidden_layer:,
  Line_Font, solid,
  Line_Width, Normal,
};

# helper object for creation of revolve
(Child) nx_axis: {
  Class, nx_axis,
  Point, nx_null();
  Direction, Direction:,
  Visibility, invisible,
  Color, 0,
  Layer, hidden_layer:,
  Line_Font, solid,
  Line_Width, Normal,
};

(Child) revolved_contour: {
  Class, nx_revolve,
  section, section:,
  Axis, nx_axis:,
  Offset_Start_Offset, 0,
  Offset_End_Offset, 5,
  Offset_Offset_Type, no_offset,
  Offset_Option, no_offset,
  Boolean_Operation_Type, create,
  Boolean_Operation_Target_Bodies, {},
  Tolerance, 0.0254,
  Limits_Limit_Opt, nonsymmetric_offset,
  Limits_Start_Extend_Target, nx_null(),
  Limits_Start_Extend_Value, 0,
  Limits_Start_Extend_Trim_Type, value,
  Limits_End_Extend_Target, nx_null(),
  Limits_End_Extend_Value, 360,
  Limits_End_Extend_Trim_Type, value,
  Limits_Symmetric_Option, FALSE,
  Feature_Options_Body_Type, sheet,
  Offset_Enabled, FALSE,
};


(Child) LayerManager: {
  Class, nx_layer_manager;
};

# sets all geometry of this class to hidden_layer:
# so the contour doesn't show up
(Any) set_object_layers:@{

 	$body << ug_feature_askBody(revolved_contour:);
  ug_setObjectLayer( $body, hidden_layer: );

	# avoid overriding user settings when re-evaluating e.g. after 
	# contour swapping
	if( ! layer_manager_intialized?:) then @{
		nx_ja_layer_set_state( LayerManager:, hidden_layer:, hidden, false);	
		StoreValue(true, self(), layer_manager_intialized?);
	} else 0;
};


# invisible point to trigger stuff
(Child) DummyPoint: {
  Class, ug_point;
  Position, Point(0,0,0);
  Layer, 257;
  demandOrder, { revolved_contour: };
	demandValue, { set_object_layers: };
};

